apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'gradle'
version = '1.0'

// Typically set in ~/.gradle/gradle.properties
project['signing.keyId'] = "24875D73"
project['signing.password'] = "gradle"
project['signing.secretKeyRingFile'] = file("secKeyRingFile.gpg").absolutePath

signing {
    sign configurations.archives
}

configurations {
    published.extendsFrom archives, signatures
}

uploadPublished {
    repositories {
        mavenDeployer {
            repository(url: uri("${buildDir}/repo"))
            
            beforeDeployment { MavenDeployment deployment ->
                def signedPomArtifact = sign(deployment.pomArtifact).singleArtifact
                // Have to alter the “type” of the artifact to match what is published
                // See http://issues.gradle.org/browse/GRADLE-1589
                signedPomArtifact.type = "pom." + signing.type.extension
                deployment.addArtifact(signedPomArtifact)
            }
        }
    }
}