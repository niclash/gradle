apply plugin: 'java'

// START SNIPPET access-source-set
// Various ways to access the main source set
println sourceSets.main.output.classesDir
println sourceSets['main'].output.classesDir
sourceSets {
    println main.output.classesDir
}
sourceSets {
    main {
        println output.classesDir
    }
}

// Iterate over the source sets
sourceSets.each {SourceSet set ->
    println set.name
}
// END SNIPPET access-source-set

// START SNIPPET define-source-set
sourceSets {
    intTest
}
// END SNIPPET define-source-set

repositories {
    mavenCentral()
}

// START SNIPPET classpath-using-configurations
configurations {
    intTestCompile { extendsFrom compile }
    intTestRuntime { extendsFrom intTestCompile, runtime }
}

sourceSets {
    intTest {
        compileClasspath = sourceSets.main.output + configurations.intTestCompile
        runtimeClasspath = output + sourceSets.main.output + configurations.intTestRuntime
    }
}
// END SNIPPET classpath-using-configurations

// START SNIPPET jar
task intTestJar(type: Jar) {
    from sourceSets.intTest.output
}
// END SNIPPET jar

// START SNIPPET javadoc
task intTestJavadoc(type: Javadoc) {
    source sourceSets.intTest.allJava
}
// END SNIPPET javadoc

// START SNIPPET test
task intTest(type: Test) {
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath
}
// END SNIPPET test
