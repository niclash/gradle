import groovy.text.SimpleTemplateEngine
import groovy.text.Template

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath gradleApi()
    }
}

repositories {
    mavenCentral()
}

configurations {
    junit
}

dependencies {
    junit 'junit:junit:4.8.2'
}

apply plugin: 'base'

task small(type: GeneratorTask, description: 'Generates a small project') {
}

task largeSrc(type: GeneratorTask, description: 'Generates a single project with lots of source files') {
    sourceFiles = 50000
    linesOfCodePerSourceFile = 20
}

task multi(type: GeneratorTask, description: 'Generates a multi-project build') {
    projects = 25
    sourceFiles = 100
}

task mixedSize(type: GeneratorTask) {
    projects = 400
    sourceFiles = 100
    projects[1].sourceFiles = 20000
}

task multiGroovy(type: GeneratorTask, description: 'Generates a multi-project groovy build') {
    projects = 25
    groovyProject = true
}

task all(dependsOn: {tasks.withType(GeneratorTask).all})

class TestProject {
    final String name
    final Object defaults
    Integer sourceFiles
    Integer linesOfCodePerSourceFile

    TestProject() {
    }

    TestProject(String name, Object defaults) {
        this.name = name
        this.defaults = defaults
    }

    int getSourceFiles() {
        return sourceFiles ?: defaults.sourceFiles
    }

    int getLinesOfCodePerSourceFile() {
        return linesOfCodePerSourceFile ?: defaults.linesOfCodePerSourceFile
    }
}

class GeneratorTask extends DefaultTask {
    @OutputDirectory
    File destDir
    boolean groovyProject
    int sourceFiles = 1
    int linesOfCodePerSourceFile = 5

    final List<TestProject> projects = []
    final SimpleTemplateEngine engine = new SimpleTemplateEngine()
    final Map<File, Template> templates = [:]

    def GeneratorTask() {
        outputs.upToDateWhen { false }
        setProjects(1)
        destDir = project.file("${project.buildDir}/${name}")
    }

    void setProjects(int projectCount) {
        if (projects.size() > projectCount) {
            projects.subList(projectCount, projects.size()).clear()
        } else {
            while (projects.size() < projectCount) {
                def project = projects.empty ? new TestProject("root", this) : new TestProject("project${projects.size()}", this)
                projects << project
            }
        }
    }

    @TaskAction
    void generate() {
        ant.delete(dir: destDir)
        destDir.mkdirs()

        generateRootProject()
        subprojects.each {
            generateSubProject(it)
        }
    }

    List getSubprojectNames() {
        return getSubprojects().collect { it.name }
    }

    TestProject getRootProject() {
        return projects[0]
    }

    List<TestProject> getSubprojects() {
        return projects.subList(1, projects.size())
    }

    def generateRootProject() {
        generateProject rootProject, subprojects: subprojectNames, projectDir: destDir,
                files: subprojectNames.empty ? [] : ['settings.gradle'],
                includeSource: subprojectNames.empty

        Configuration configuration = project.configurations.junit
        project.copy {
            from configuration
            into new File(getDestDir(), 'lib/test')
        }
    }

    def generateSubProject(TestProject testProject) {
        generateProject testProject, subprojects: [], projectDir: new File(destDir, testProject.name), files: [],
                includeSource: true
    }

    def generateProject(Map args, TestProject testProject) {
        File projectDir = args.projectDir
        println "Generating $testProject.name"

        List files = args.files + [
                'build.gradle',
                'pom.xml',
                'build.xml',
        ]

        Closure generate = {String name, String templateName, Map templateArgs ->
            File destFile = new File(projectDir, name)
            File srcTemplate = project.file("src/templates/$templateName")
            destFile.parentFile.mkdirs()
            destFile.withWriter {Writer writer ->
                getTemplate(srcTemplate).make(templateArgs).writeTo(writer)
            }
        }

        args += [projectName: testProject.name, groovyProject: groovyProject, propertyCount: (testProject.linesOfCodePerSourceFile.intdiv(7))]

        files.each {String name ->
            generate(name, name, args)
        }

        if (args.includeSource) {
            testProject.sourceFiles.times {
                String packageName = "org.gradle.test.performance${(int) (it / 100) + 1}"
                Map classArgs = args + [packageName: packageName, productionClassName: "Production${it + 1}", testClassName: "Test${it + 1}"]
                generate("src/main/java/${packageName.replace('.', '/')}/${classArgs.productionClassName}.java", 'Production.java', classArgs)
                generate("src/test/java/${packageName.replace('.', '/')}/${classArgs.testClassName}.java", 'Test.java', classArgs)
            }
            if (groovyProject) {
                testProject.sourceFiles.times {
                    String packageName = "org.gradle.test.performance${(int) (it / 100) + 1}"
                    Map classArgs = args + [packageName: packageName, productionClassName: "ProductionGroovy${it + 1}", testClassName: "TestGroovy${it + 1}"]
                    generate("src/main/groovy/${packageName.replace('.', '/')}/${classArgs.productionClassName}.groovy", 'Production.groovy', classArgs)
                    generate("src/test/groovy/${packageName.replace('.', '/')}/${classArgs.testClassName}.groovy", 'Test.groovy', classArgs)
                }
            }
        }
    }

    def getTemplate(File srcTemplate) {
        def template = templates[srcTemplate]
        if (!template) {
            template = engine.createTemplate(srcTemplate)
            templates[srcTemplate] = template
        }
        return template
    }
}
